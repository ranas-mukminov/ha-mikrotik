# ============================================================================
# ha_pushbackup.script - Configuration and File Synchronization Script
# ============================================================================
# Purpose: Push configuration backup and all files from active to standby
# Tested RouterOS: 6.44.6
#
# DANGEROUS OPERATIONS:
#   - REMOVES ALL NON-HA FILES on standby via HA_mkdirs.auto.rsc (line 20)
#   - Creates and uploads backup that triggers REBOOT on standby (line 64-71)
#   - Transfers all files including sensitive data
#   - Uses FTP for file transfer (password in plaintext over network)
#
# Behavior:
#   - Only runs if router is master
#   - Creates directory structure on standby
#   - Purges all non-HA files from standby
#   - Transfers all files from active to standby
#   - Creates encrypted backup and triggers restore on standby (causes reboot)
#
# Prerequisites:
#   - Router must be in master/active role
#   - Standby must be reachable via HA network
#   - FTP service must be enabled on standby
# ============================================================================

:if ([:len [/system script job find where script="ha_pushbackup"]] > 1) do={:error "already running pushbackup"; }
:global haPassword
:global isMaster
:global haAddressOther
:if (!$isMaster) do={
   :error "NOT MASTER"
} else={
   #Really? this is the only way to create directories?
   :local mkdirCode ":do { /ip smb shares add comment=HA_AUTO name=mkdir disabled=yes directory=/skins } on-error={}"

   :foreach k in=[/file find type="directory"] do={
      :local xferfile [/file get $k name]
      if ([:pick "$xferfile" 0 3] != "HA_") do={
         :set mkdirCode "$mkdirCode\r\n/ip smb shares set [find comment=HA_AUTO] directory=\"$xferfile\""
      }
   }

   :set mkdirCode "$mkdirCode\r\n/ip smb shares remove [find comment=HA_AUTO]\r\n"
   # DANGEROUS: This code will delete all non-HA files on standby to keep files in sync
   # This reduces NVRAM wear by avoiding incremental updates, but removes all existing files
   :local purgeFilesCode ":foreach k in=[/file find type!=\"directory\"] do={ :local xferfile [/file get \$k name]; if ([:pick \"\$xferfile\" 0 3] != \"HA_\") do={ :put \"removing \$xferfile\"; /file remove \$k; } }"
   :set mkdirCode "$purgeFilesCode;\r\n/delay 2;\r\n$mkdirCode"

   /file print file=HA_mkdirs.txt
   /file set [find name="HA_mkdirs.txt"] contents=$mkdirCode
   :put "mkdirCode: $mkdirCode end_mkDirCode"
   /tool fetch upload=yes src-path=HA_mkdirs.txt dst-path=HA_mkdirs.auto.rsc address=$haAddressOther user=ha password=$haPassword mode=ftp 
   
   :foreach k in=[/file find type!="directory"] do={
      :local xferfile [/file get $k name]
      if ([:pick "$xferfile" 0 3] != "HA_") do={
         :put "Transferring: $xferfile"
         :do {
            /tool fetch upload=yes src-path=$xferfile dst-path=$xferfile address=$haAddressOther user=ha password=$haPassword mode=ftp
         } on-error={
            :put "Failed to transfer $xferfile"
         }
      }
   }

   :if ([:len [/file find name=HA_dsa]] <= 0) do={ 
      /ip ssh export-host-key key-file-prefix="HA"
   }

:if ([:len [/file find name="HA_dsa"]] = 1) do={
   /tool fetch upload=yes src-path=HA_dsa dst-path=HA_dsa address=$haAddressOther user=ha password=$haPassword mode=ftp
}
:if ([:len [/file find name="HA_rsa"]] = 1) do={
   /tool fetch upload=yes src-path=HA_rsa dst-path=HA_rsa address=$haAddressOther user=ha password=$haPassword mode=ftp
}
# bugfix v7
:if ([:len [/file find name="HA_dsa.pem"]] = 1) do={
   /tool fetch upload=yes src-path=HA_dsa.pem dst-path=HA_dsa.pem address=$haAddressOther user=ha password=$haPassword mode=ftp
}
:if ([:len [/file find name="HA_rsa.pem"]] = 1) do={
   /tool fetch upload=yes src-path=HA_rsa.pem dst-path=HA_rsa.pem address=$haAddressOther user=ha password=$haPassword mode=ftp
}

   :global haMasterConfigVer
   [/system script run [find name="ha_setconfigver"]]
   /file print file=HA_run-after-hastartup.txt
   /file set [find name=HA_run-after-hastartup.txt] contents=":global haConfigVer \"$haMasterConfigVer\""
   /tool fetch upload=yes src-path=HA_run-after-hastartup.txt dst-path=HA_run-after-hastartup.rsc address=$haAddressOther user=ha password=$haPassword mode=ftp 

   /export file=HA_b2s.rsc
   /system backup save name=HA_b2s.backup password=p
   /tool fetch upload=yes src-path=HA_b2s.rsc dst-path=HA_b2s.rsc address=$haAddressOther user=ha password=$haPassword mode=ftp  
   /tool fetch upload=yes src-path=HA_b2s.backup dst-path=HA_b2s.backup address=$haAddressOther user=ha password=$haPassword mode=ftp  
   # DANGEROUS: Uploading restore script will trigger automatic backup load on standby
   # This will cause the standby router to REBOOT and apply the new configuration
   /file print file=HA_restore-backup.rsc; /file set [find name="HA_restore-backup.rsc.txt"] contents="/system backup load name=HA_b2s.backup password=p"
   :do {
      /tool fetch upload=yes src-path=HA_restore-backup.rsc.txt dst-path=HA_restore-backup.auto.rsc address=$haAddressOther user=ha password=$haPassword mode=ftp 
   } on-error={
      :put "OK - status failed is OK from last fetch, standby is rebooting."
   }
}
